
apply plugin: 'java'
apply plugin: 'maven-publish'

version = '1.0_beta2'

tasks.register('copyNatives', Copy) {
	group = 'build'
	description = 'Copy natives from Bin and External folder'
	
	from '../Binaries/electronflow_jni_x64.dll', '../Binaries/nglink_x64.dll', '../External/ngspice42_x64.dll'
	into 'src/main/resources/natives'
}

// Add additional content to the manifest
tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': version,
                   'Author': 'M_Marvin')
    }
}

java {
	sourceCompatibility = "1.17"
	targetCompatibility = "1.17"
    withSourcesJar()
}

processResources.dependsOn('copyNatives')
sourcesJar.dependsOn('copyNatives')

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/m-marvin/library-electronflow")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
        	groupId = 'de.m_marvin.electronflow'
        	artifactId = "jelectronflow"
        	
            from components.java
        }
    }
}
